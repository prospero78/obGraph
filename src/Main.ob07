MODULE Main; (* Пример передвижения черепашки *)
 (* Демонстрация использования таймауту и select() 
	для отображения и перемещения черепашки в окне X11 *)

IMPORT SYSTEM, 
	mGr := xgraph,
	mSqr := square;

CONST
	Side = 8;  (* nr of pixels of a square side *)

VAR 
	currentX :INTEGER;
	square: mSqr.TSquare;

PROCEDURE DrawAttr(color, x, y :INTEGER); (* отрисовывает квадрат с нужным цветом, в нужной позиции *)
	BEGIN
		mSqr.ColorSet(square, color);
		mSqr.CoordSet(square, x, y);
		mSqr.Draw(square)
	END DrawAttr;

PROCEDURE linePut (x : INTEGER; y: INTEGER;sample : ARRAY OF CHAR); (* рисует строковую линию из состава черепашки *)
	VAR z, x1: INTEGER;
	BEGIN
		FOR z := 0 TO LEN(sample) - 1 DO
			x1 := (x + z) MOD 100;
			IF sample[z] = "b" THEN
				DrawAttr(0600000H, x1, y)
			END;  (* brown *)
			IF sample[z] = "g" THEN
				DrawAttr(000C000H, x1, y)
			END;  (* green *)
			IF sample[z] = "r" THEN
				DrawAttr(0FF0000H, x1, y)
			END;  (* red *)
		END;
	END linePut;

PROCEDURE turtlePicture (x , y : INTEGER);(* Рисует картинку черепашки ил строк и литер *)
	BEGIN
		linePut(x, y + 0 , "....bb........");
		linePut(x, y + 1 , "....bbb.......");
		linePut(x, y + 2 , "....bbbb......");
		linePut(x, y + 3 , ".bb..bbb......");
		linePut(x, y + 4 , ".bgggbbbgbbgb.");
		linePut(x, y + 5 , ".gggrrrggbbbb.");
		linePut(x, y + 6 , "bggrrrrggbbbbg");
		linePut(x, y + 7 , ".ggggggg......");
		linePut(x, y + 8 , ".bb..bbb......");
		linePut(x, y + 9 , "....bbbb......");
		linePut(x, y + 10, ".....bbb......");
		linePut(x, y + 11, ".....bb.......")
	END turtlePicture;

PROCEDURE screenUpdate;(* Отрисовка всего кадра *)
	BEGIN
		mGr.Begin;
		mGr.clear (0C0F0FFH);  (* light blue *)
		turtlePicture (currentX, 15);
		mGr.screenEnd;
	END screenUpdate;

PROCEDURE MainLoop; (* Главный цикл работы анимации *)
	VAR
		isStop :BOOLEAN;
		ev :mGr.EventPars;
		ch :CHAR;
	BEGIN
		isStop := FALSE; currentX := 15;
		square := mSqr.New(Side);
		screenUpdate;
		REPEAT
			mGr.nextEvent (400, ev);
			IF ev[0] = mGr.EventTimeOut THEN
				screenUpdate;
				INC (currentX, 4);
			ELSIF ev[0] = mGr.EventKeyPressed THEN
				ch := CHR(ev[4]);
				IF (ch = "q") OR (ch = 0AX) OR (ch = " ") THEN
					isStop := TRUE
				END;
				IF ev[2] = 9 (* ESC *) THEN
					isStop := TRUE
				END;
			END;
		UNTIL isStop;
		mGr.Finish
	END MainLoop;

BEGIN
	MainLoop;
END Main.
